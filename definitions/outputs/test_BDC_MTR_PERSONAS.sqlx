config {
  type: "incremental",
  database: "askasks-poc-master",
  schema: "ds_poc_oracle",
  bigquery: {
    partitionBy: "FECHA_DATO"
  },
  tags: ["ingestion_tables_config"],
  dependencies: ["test_BDC_MTR_PERSONAS_alter_table"]
}

pre_operations {
  DECLARE fecha_dato_checkpoint DEFAULT (
    ${when(incremental(),
      `SELECT IFNULL(MAX(FECHA_DATO), DATE("1999-12-31")) FROM ${self()}`,
      `SELECT DATE("1999-12-31")`)}
  );
  
  ${when(incremental(),
    `-- Handle if the Dataform is called several times during the same day
    IF (fecha_dato_checkpoint = (SELECT CURRENT_DATE() - 1)) THEN
      DELETE FROM ${self()}
        WHERE FECHA_DATO = fecha_dato_checkpoint;
      SET fecha_dato_checkpoint = (SELECT IFNULL(MAX(FECHA_DATO), DATE("1999-12-31")) FROM ${self()});
    END IF`)}
}

SELECT
  *
FROM
  ${ref({ database: "askasks-poc-raw", schema: "ds_poc_oracle", name: "test_BDC_MTR_PERSONAS" })}
WHERE
  FECHA_DATO > fecha_dato_checkpoint
